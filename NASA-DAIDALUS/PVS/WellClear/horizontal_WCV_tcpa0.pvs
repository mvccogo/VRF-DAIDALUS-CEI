% DAIDALUS (Detect and Avoid Alerting Logic for Unmanned Systems)
%
% Notices:
% Copyright 2016 United States Government as represented by the
% Administrator of the National Aeronautics and Space Administration. No
% copyright is claimed in the United States under Title 17,
% U.S. Code. All Other Rights Reserved.
% Disclaimers
% No Warranty: THE SUBJECT SOFTWARE IS PROVIDED "AS IS" WITHOUT ANY
% WARRANTY OF ANY KIND, EITHER EXPRESSED, IMPLIED, OR STATUTORY,
% INCLUDING, BUT NOT LIMITED TO, ANY WARRANTY THAT THE SUBJECT SOFTWARE
% WILL CONFORM TO SPECIFICATIONS, ANY IMPLIED WARRANTIES OF
% MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE, OR FREEDOM FROM
% INFRINGEMENT, ANY WARRANTY THAT THE SUBJECT SOFTWARE WILL BE ERROR
% FREE, OR ANY WARRANTY THAT DOCUMENTATION, IF PROVIDED, WILL CONFORM TO
% THE SUBJECT SOFTWARE. THIS AGREEMENT DOES NOT, IN ANY MANNER,
% CONSTITUTE AN ENDORSEMENT BY GOVERNMENT AGENCY OR ANY PRIOR RECIPIENT
% OF ANY RESULTS, RESULTING DESIGNS, HARDWARE, SOFTWARE PRODUCTS OR ANY
% OTHER APPLICATIONS RESULTING FROM USE OF THE SUBJECT SOFTWARE.
% FURTHER, GOVERNMENT AGENCY DISCLAIMS ALL WARRANTIES AND LIABILITIES
% REGARDING THIRD-PARTY SOFTWARE, IF PRESENT IN THE ORIGINAL SOFTWARE,
% AND DISTRIBUTES IT "AS IS."
% Waiver and Indemnity: RECIPIENT AGREES TO WAIVE ANY AND ALL CLAIMS
% AGAINST THE UNITED STATES GOVERNMENT, ITS CONTRACTORS AND
% SUBCONTRACTORS, AS WELL AS ANY PRIOR RECIPIENT.  IF RECIPIENT'S USE OF
% THE SUBJECT SOFTWARE RESULTS IN ANY LIABILITIES, DEMANDS, DAMAGES,
% EXPENSES OR LOSSES ARISING FROM SUCH USE, INCLUDING ANY DAMAGES FROM
% PRODUCTS BASED ON, OR RESULTING FROM, RECIPIENT'S USE OF THE SUBJECT
% SOFTWARE, RECIPIENT SHALL INDEMNIFY AND HOLD HARMLESS THE UNITED
% STATES GOVERNMENT, ITS CONTRACTORS AND SUBCONTRACTORS, AS WELL AS ANY
% PRIOR RECIPIENT, TO THE EXTENT PERMITTED BY LAW.  RECIPIENT'S SOLE
% REMEDY FOR ANY SUCH MATTER SHALL BE THE IMMEDIATE, UNILATERAL
% TERMINATION OF THIS AGREEMENT.

horizontal_WCV_tcpa0[TCPA:nnreal,DMOD,HMD:posreal]  : THEORY 

% TCPA is the time to closest point of approach threshold
% DMOD is the distance thershold

BEGIN

  IMPORTING vect_analysis@vect_cont_2D,
	    vect_analysis@vect2_cont_dot[real],
            vectors@det_2D,
	    ACCoRD@cd2d,
	    analysis@polynomial_deriv,
	    ACCoRD@Lookahead

  so,vo,
  si,vi,
  s,v : VAR Vect2  
  r     : VAR real
  p     : VAR posreal
  T	: VAR posreal

  % You are not well clear if either inside DMOD or the dist at tca is at most HMD and
  % the time to TCA is at most TCPA

  horizontal_WCV_tcpa0(s,v): bool =
    sqv(s) <= sq(DMOD) OR
    (sqv(v)/=0 AND
     sqv(s + horizontal_tca(s,v)*v)<=sq(HMD) AND
    s*v<=0 AND % Aircraft are converging (getting closer together)
    horizontal_tca(s,v)<=TCPA)

  horizontal_WCV_tcpa0_interval(T,s,v):[# entry:Lookahead[0,T],exit:Lookahead[0,T] #] =
    IF    sqv(v) = 0 AND sqv(s)<=sq(DMOD) THEN (# entry:=0, exit:=T #)
    ELSIF sqv(v)=0 THEN (# entry:=T,exit:=0 #) % detection returns false
    ELSIF sqv(s)<=sq(DMOD) THEN (# entry:=0,exit:=min(T,Theta_D[DMOD](s,v,1)) #)
    ELSIF s*v>0 THEN (# entry:=T,exit:=0 #) % detection returns false
    ELSIF sqv(s + horizontal_tca(s,v)*v)>sq(HMD) AND sqv(s + horizontal_tca(s,v)*v)<=sq(DMOD)
    	               AND Theta_D[DMOD](s,v,-1)<=T
    	  THEN (# entry:= min(T,max(0,Theta_D[DMOD](s,v,-1))), exit:= min(T,Theta_D[DMOD](s,v,1)) #)
    ELSIF sqv(s + horizontal_tca(s,v)*v)>sq(HMD) AND sqv(s + horizontal_tca(s,v)*v)<=sq(DMOD)
          THEN (# entry:=T,exit:=0 #) % detection returns false
    ELSIF sqv(s + horizontal_tca(s,v)*v)>sq(HMD) THEN (# entry:=T,exit:=0 #) % detection returns false
    ELSIF Delta[DMOD](s,v)<0 AND horizontal_tca(s,v)-TCPA>T 
      THEN (# entry:=T,exit:=0 #) % detection returns false
    ELSIF Delta[DMOD](s,v)<0 THEN 
         (# entry:=max(0,horizontal_tca(s,v)-TCPA), 
    	     exit:=min(T,horizontal_tca(s,v)) #)
    ELSE LET tmin = min(Theta_D[DMOD](s,v,-1),horizontal_tca(s,v)-TCPA) IN
      IF tmin > T THEN (# entry:=T,exit:=0 #) % detection returns false
      ELSE  (# entry:= max(0,tmin), exit:=min(T,Theta_D[DMOD](s,v,1)) #)
      ENDIF
    ENDIF

  horizontal_WCV_tcpa0_interval_def: LEMMA FORALL (t:Lookahead[0,T]):
    LET nwcentryexit = horizontal_WCV_tcpa0_interval(T,s,v),
        (nwcentry,nwcexit) = (nwcentryexit`entry,nwcentryexit`exit)
    IN
      horizontal_WCV_tcpa0(s+t*v,v) IFF
      (nwcentry<=t AND t<=nwcexit)
    
END horizontal_WCV_tcpa0
