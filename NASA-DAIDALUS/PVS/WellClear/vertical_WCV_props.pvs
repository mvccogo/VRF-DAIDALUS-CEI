% DAIDALUS (Detect and Avoid Alerting Logic for Unmanned Systems)
%
% Notices:
% Copyright 2016 United States Government as represented by the
% Administrator of the National Aeronautics and Space Administration. No
% copyright is claimed in the United States under Title 17,
% U.S. Code. All Other Rights Reserved.
% Disclaimers
% No Warranty: THE SUBJECT SOFTWARE IS PROVIDED "AS IS" WITHOUT ANY
% WARRANTY OF ANY KIND, EITHER EXPRESSED, IMPLIED, OR STATUTORY,
% INCLUDING, BUT NOT LIMITED TO, ANY WARRANTY THAT THE SUBJECT SOFTWARE
% WILL CONFORM TO SPECIFICATIONS, ANY IMPLIED WARRANTIES OF
% MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE, OR FREEDOM FROM
% INFRINGEMENT, ANY WARRANTY THAT THE SUBJECT SOFTWARE WILL BE ERROR
% FREE, OR ANY WARRANTY THAT DOCUMENTATION, IF PROVIDED, WILL CONFORM TO
% THE SUBJECT SOFTWARE. THIS AGREEMENT DOES NOT, IN ANY MANNER,
% CONSTITUTE AN ENDORSEMENT BY GOVERNMENT AGENCY OR ANY PRIOR RECIPIENT
% OF ANY RESULTS, RESULTING DESIGNS, HARDWARE, SOFTWARE PRODUCTS OR ANY
% OTHER APPLICATIONS RESULTING FROM USE OF THE SUBJECT SOFTWARE.
% FURTHER, GOVERNMENT AGENCY DISCLAIMS ALL WARRANTIES AND LIABILITIES
% REGARDING THIRD-PARTY SOFTWARE, IF PRESENT IN THE ORIGINAL SOFTWARE,
% AND DISTRIBUTES IT "AS IS."
% Waiver and Indemnity: RECIPIENT AGREES TO WAIVE ANY AND ALL CLAIMS
% AGAINST THE UNITED STATES GOVERNMENT, ITS CONTRACTORS AND
% SUBCONTRACTORS, AS WELL AS ANY PRIOR RECIPIENT.  IF RECIPIENT'S USE OF
% THE SUBJECT SOFTWARE RESULTS IN ANY LIABILITIES, DEMANDS, DAMAGES,
% EXPENSES OR LOSSES ARISING FROM SUCH USE, INCLUDING ANY DAMAGES FROM
% PRODUCTS BASED ON, OR RESULTING FROM, RECIPIENT'S USE OF THE SUBJECT
% SOFTWARE, RECIPIENT SHALL INDEMNIFY AND HOLD HARMLESS THE UNITED
% STATES GOVERNMENT, ITS CONTRACTORS AND SUBCONTRACTORS, AS WELL AS ANY
% PRIOR RECIPIENT, TO THE EXTENT PERMITTED BY LAW.  RECIPIENT'S SOLE
% REMEDY FOR ANY SUCH MATTER SHALL BE THE IMMEDIATE, UNILATERAL
% TERMINATION OF THIS AGREEMENT.

vertical_WCV_props: THEORY
BEGIN

  IMPORTING vertical_WCV

  sz,vz   : VAR real
  nzvz    : VAR nzreal
  B	  : VAR nnreal
  T	  : VAR posreal
  ZTHR    : VAR posreal
  tcoa1,
  tcoa2   : VAR nnreal

  vertical_entry_tcoa_ge_0 : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,vz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,vz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry IN
      	nonempty?(interval_tcoa1) IMPLIES
          nonempty?(interval_tcoa2) AND 0 <= delta 

  vertical_entry_tcoa_singleton : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,vz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,vz) IN
        nonempty?(interval_tcoa1) AND
      	singleton?(interval_tcoa2) IMPLIES
        interval_tcoa1`entry = interval_tcoa2`entry AND
        interval_tcoa1`exit = interval_tcoa2`exit 

  Theta_H_ge2 : LEMMA
    FORALL (zthr1,zthr2:posreal):
      zthr1 <= zthr2 IMPLIES
      Theta_H[zthr1](sz,nzvz,Entry) >= Theta_H[zthr2](sz,nzvz,Entry)

  vertical_entry_tcoa_vz_eq_0 : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,vz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,vz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry IN
      nonempty?(interval_tcoa1) AND
      vz = 0 IMPLIES
        delta = 0

  vertical_entry_tcoa_leq_a : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,nzvz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,nzvz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry,
        theta1 = Theta_H[max(ZTHR, abs(nzvz)*tcoa1)](sz, nzvz,Entry) IN
      nonempty?(interval_tcoa1) AND
      theta1 <= B IMPLIES
        delta = 0

  vertical_entry_tcoa_leq_b : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,nzvz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,nzvz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry,
        theta1 = Theta_H[max(ZTHR, abs(nzvz)*tcoa1)](sz, nzvz,Entry),
        theta2 = Theta_H[max(ZTHR, abs(nzvz)*tcoa2)](sz, nzvz,Entry) IN
      nonempty?(interval_tcoa1) AND
      theta2 > B IMPLIES
        delta = theta1-theta2

  vertical_entry_tcoa_leq_c : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,nzvz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,nzvz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry,
        theta1 = Theta_H[max(ZTHR, abs(nzvz)*tcoa1)](sz, nzvz,Entry),
        theta2 = Theta_H[max(ZTHR, abs(nzvz)*tcoa2)](sz, nzvz,Entry) IN
      nonempty?(interval_tcoa1) AND
      theta2 <= B AND B < theta1 IMPLIES
        delta = theta1 - B

  Theta_H_sub2 : LEMMA
    FORALL (zthr1,zthr2:posreal):
      Theta_H[zthr1](sz,nzvz,Entry) - Theta_H[zthr2](sz,nzvz,Entry) = (zthr2-zthr1)/abs(nzvz)

  vertical_entry_tcoa_leq : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,vz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,vz),
	delta = interval_tcoa1`entry-interval_tcoa2`entry IN
      nonempty?(interval_tcoa1) IMPLIES
        0 <= delta  AND
        delta <= IF vz = 0 THEN 0 ELSE 
                   (max(ZTHR, abs(vz)*tcoa2) - max(ZTHR, abs(vz)*tcoa1))/abs(vz) 
                 ENDIF

  vertical_exit_tcoa_eq_0 : LEMMA B < T AND tcoa1 <= tcoa2 IMPLIES
    LET interval_tcoa1 = vertical_WCV_interval[tcoa1,ZTHR](B,T,sz,vz),
      	interval_tcoa2 = vertical_WCV_interval[tcoa2,ZTHR](B,T,sz,vz),
	delta = interval_tcoa1`exit-interval_tcoa2`exit IN
      nonempty?(interval_tcoa1) IMPLIES
        delta = 0

END vertical_WCV_props
